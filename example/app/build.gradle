apply plugin: 'com.android.application'
apply plugin: 'jacoco'
apply plugin: 'spoon'

android {
  compileSdkVersion 21
  buildToolsVersion "23.0.3"

  defaultConfig {
    minSdkVersion 8
    targetSdkVersion 21

    versionCode 1
    versionName '1.0'

    testApplicationId "com.stanfy.spoon.example.test"
    // We use custom test runner in this example to check how instrumentation arguments can be passed.
    // You can omit this configuration in your project build.gradle.
    testInstrumentationRunner "com.stanfy.spoon.example.test.CustomTestRunner"
  }

  buildTypes {
    debug {
      testCoverageEnabled = true
    }
  }

  productFlavors {
    free {

    }
    /*pro {

    }*/
  }

}

def coverageSourceDirs = [
        'src/main/java'
]

task jacocoTestReport(type: JacocoReport, dependsOn: "spoon") {
  group = "Reporting"
  description = "Generate Jacoco coverage reports after running tests."
  reports {
    //xml.enabled = true
    html.enabled = true
  }
  //getReports().getHtml().setDestination(file("$buildDir/reports/jacoco/html"))
  classDirectories = fileTree(
          dir: 'build/intermediates/classes/free',
          includes: ['**/com/**'],
          excludes: ['**/R.class',
                     '**/Manifest.class',
                     '**/Manifest*.class',
                     '**/BuildConfig.class',
          ])
  sourceDirectories = files(coverageSourceDirs)
  executionData = files("$buildDir/custom-report-dir/free/debug/coverage/merged-coverage.ec")
  // Bit hacky but fixes https://code.google.com/p/android/issues/detail?id=69174.
  // We iterate through the compiled .class tree and rename $$ to $.
  doFirst {
    new File("$buildDir/intermediates/classes/").eachFileRecurse { file ->
      if (file.name.contains('$$')) {
        file.renameTo(file.path.replace('$$', '$'))
      }
    }
  }
}

spoon {

  // Enable debug output
  debug = true

  // Set custom test reports directory (defaults to "spoon")
  baseOutputDir = file("$buildDir/custom-report-dir")
  srcDir = file("src/")
  reportDir = file("reports/")

  // Enable setting test class/method-to-be-run from command line. E.g.:
  // $> ../gradlew spoonFreeDebugTest -PspoonClassName=com.stanfy.spoon.example.test.MainActivityTest -PspoonMethodName=testSetText
  if (project.hasProperty('spoonClassName')) {
    className = project.spoonClassName

    if (project.hasProperty('spoonMethodName')) {
      methodName = project.spoonMethodName
    }
  }

  // You can pass instrumentation arguments. Tests in this example assert this pair is passed.
  instrumentationArgs = ["foo=bar"]

  // It's also possible to configure tests sharding using convenient properties:
  // numShards = 2
  // shardIndex = 0

  // devices = ["emulator-5554"]
  adbTimeout = 30

  codeCoverage = true

  grantAllPermissions = true
  shard = true
  smartShard = true
}

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  androidTestCompile "com.squareup.spoon:spoon-client:1.7.1-SmartShard"
}

// for integration test of this plugin
check << {
  println "Checking spoon tasks..."
  assert project.tasks.spoon != null
  assert project.tasks.spoonFreeDebugTest != null
  assert project.tasks.spoonProDebugTest != null

  assert project.tasks.spoonFreeDebugTest.debug
  assert "$project.tasks.spoonFreeDebugTest.output".startsWith("$buildDir/custom-report-dir")

  assert project.tasks.spoonFreeDebugTest.codeCoverage

  println "SUCCESS"
}
